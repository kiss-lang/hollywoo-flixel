(loadFrom "hollywoo-flixel" "src/hollywoo_flixel/Aliases.kiss")

(prop :StageFacing defaultFacing)

(prop :FuzzyMap<String> animationNames (new FuzzyMap<String>))

(prop :Float z 0)

(prop &mut :Bool portraitFacingForward false)

(defNew [&prop :String assetPath &opt frameWidth frameHeight :StageFacing defaultFacing :Map<String,AnimationArgs> animations]
    (super)

    (set this.defaultFacing (or defaultFacing FacingRight))

    (if (and frameWidth frameHeight)
            (loadGraphic assetPath true frameWidth frameHeight)
        (loadGraphic assetPath))
    (when animations
        (doFor =>name animationArgs animations
            (dictSet animationNames name name)
            (when (Prelude.isNull animationArgs.looped)
                (set animationArgs.looped true))
            (animation.add
                name
                animationArgs.frames
                (or animationArgs.frameRate 30)
                animationArgs.looped
                ?animationArgs.flipX
                ?animationArgs.flipY))))

(method :Void playAnimation [fuzzySearchKey &opt :Void->Void cc]
    (if (animationNames.exists fuzzySearchKey)
            {
                (when cc 
                    (assert !animation.finishCallback)
                    (set animation.finishCallback ->_ {(set animation.finishCallback null)(cc)}))
                (animation.play (dictGet animationNames fuzzySearchKey) true)
            }
        {
            (#when debug
                (print "WARNING! No animation matches $fuzzySearchKey for actor with spritesheet $assetPath"))
            (when cc (cc))
        }))

(method :Void stopAnimation []
    (set animation.finishCallback null)
    (animation.stop))